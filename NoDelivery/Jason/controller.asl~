/* ----------------- Initial Beliefs ----------------- */

//none

/* ----------------- Rules ----------------- */

allClientsWaiting(NC) :- .count(waiting(_), NF) &								//all clients placed the orders if NC = NF
						  NC = NF.

allClientsFinished(NC) :- .count(finished(_), NF) &								//all clients finished the orders if NC = NF
						  NC = NF.
						  
 
/* ----------------- Initial Goals ----------------- */

!getNClients. 																	//get the total number of clients
!register.																		//register as a client


/* ----------------- Plans ----------------- */

//register
+!register
	<- .df_register(controller);												//register as controller
	   .print("Hi, I am the controller").										//

//get number of clients
+!getNClients
	<- .wait(100);																//wait for clients to register
	   .df_search(client, LC);													//search for clients
	   +nClients(.length(LC)).													//add belief for the number of clients

//client tells that all his orders were placed
+allOrdersPlaced[source(A)] 
	<-  .print(A," placed all its orders.");									//
		.abolish(allOrdersPlaced[source(A)]);									//clear memory
		+waiting(A);															//add belief for waiting client
		!checkAllClientsWaiting.												//check if all clients are waiting   
	   
//client tells that all his orders were received
+allOrdersReceived[source(A)] 
	<-  .print(A," received all its orders.");									//
		-allOrdersReceived[source(A)]; 											//clear memory
		+finished(A);															//add belief for finished client
		!checkAllClientsFinished.												//check if all clients have finished

//if all clients are waiting for their orders
+!checkAllClientsWaiting
	: nClients(NC) &															//given number of clients
	  allClientsWaiting(NC)														//if all clients are waiting for their orders
	<- .print(" ------------------------ CNP 1 COMPLETED ------------------------ "). //
	
//if not
+!checkAllClientsWaiting.

//if all clients have received their orders
+!checkAllClientsFinished
	: nClients(NC) &															//given number of clients
	  allClientsFinished(NC)													//if all clients have received their orders
	<- .print(" ------------------------ CNP 2 COMPLETED ------------------------ "). //																	
	   //.stopMAS.																//stop MAS
	   
//if not
+!checkAllClientsFinished.
	

